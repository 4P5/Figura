plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version + "+" + project.minecraft_version
group = project.maven_group
fabric_version = project.fabric_version + "+" + project.minecraft_version

repositories {
	maven { url 'https://jitpack.io' }
}

dependencies {
	// Minecraft
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Libraries
	include(implementation('com.github.Moonlight-MC:JNLua:-SNAPSHOT'))
	include(implementation('org.java-websocket:Java-WebSocket:1.5.2'))

	// Fabric API modules
	modImplementation(fabricApi.module("fabric-key-binding-api-v1", fabric_version))
	modImplementation(fabricApi.module("fabric-resource-loader-v0", fabric_version))
	modImplementation(fabricApi.module("fabric-lifecycle-events-v1", fabric_version))
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = Integer.parseInt(sourceCompatibility)
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
}
